package MobileApplication
public
	with Base_Types, Shared;
	
	device username_field
		features
			username: out data port Base_Types::String;
	end username_field;
	
	device password_field
		features
			password: out data port Base_Types::String;
	end password_field;
	
	device rember_me_field
			features
					rememberMe:	out data port Base_Types::Boolean;
	end rember_me_field;
	
	subprogram login_request_generator_function
		features
			username: in parameter Base_Types::String;
			password: in parameter Base_Types::String; 
			login_request: out parameter Shared::user_login_request.impl;
	end login_request_generator_function;
	
	subprogram implementation login_request_generator_function.impl
		connections
			populate_username: parameter username -> login_request.username;
			populate_password: parameter password -> login_request.password;
	end login_request_generator_function.impl;
	
	thread login_request_generator_thread
		features
			username: in data port Base_Types::String;
			password: in data port Base_Types::String;
			rememberMe: in data port Base_Types::Boolean;
			login_request: out data port Shared::user_login_request.impl;
	end login_request_generator_thread;
	
	thread implementation login_request_generator_thread.impl
		calls
			functions: {
				login_request_generator_function: subprogram login_request_generator_function.impl;
			};
		connections
			username_to_function: parameter username -> login_request_generator_function.username;
			password_to_function: parameter password -> login_request_generator_function.password;
			function_to_login_request: parameter login_request_generator_function.login_request -> login_request;
	end login_request_generator_thread.impl;
	
	process login_request_generator_process
		features
			username: in data port Base_Types::String;
			password: in data port Base_Types::String;
			rememberMe: in data port Base_Types::Boolean;
			login_request: out data port Shared::user_login_request.impl;
	end login_request_generator_process;
	
	process implementation login_request_generator_process.impl
		subcomponents
			login_request_generator_thread: thread login_request_generator_thread.impl;
		connections
			username_to_thread: port username -> login_request_generator_thread.username;
			password_to_thread: port password -> login_request_generator_thread.password;
			remember_me_to_thread: port rememberMe -> login_request_generator_thread.rememberMe;
			thread_to_login_request: port login_request_generator_thread.login_request -> login_request;
	end login_request_generator_process.impl;
	
	system login_request_generator
		features
			login_request: out data port Shared::user_login_request.impl;
	end login_request_generator;
	
	system implementation login_request_generator.impl
		subcomponents
			username_field: device username_field;
			password_field: device password_field;
			remember_me_field: device rember_me_field;
			
			login_request_generator_process: process login_request_generator_process.impl;
		connections
			username_field_to_process: port username_field.username -> login_request_generator_process.username;
			password_field_to_process: port password_field.password -> login_request_generator_process.password;
			remember_me_field_to_process: port remember_me_field.rememberMe -> login_request_generator_process.rememberMe;
			login_request_out: port login_request_generator_process.login_request -> login_request;
	end login_request_generator.impl;
	
	--------------------------------------------------
	
end MobileApplication;